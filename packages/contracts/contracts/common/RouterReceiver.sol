//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {AccessControlUpgradeable} from '@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol';
import {IRouterReceiver} from './IRouterReceiver.sol';

/**
 * @dev Implements OpenGSN Config
 */
contract RouterReceiver is AccessControlUpgradeable, IRouterReceiver {
    bytes32 internal constant ROUTER_ROLE = keccak256('ROUTER_ROLE');

    /**
     * @dev RouterReceiver chained initialization
     * @param _router router role
     */
    function __RouterReceiver_init(address _router) internal {
        __RouterReceiver_init_unchained(_router);
    }

    /**
     * @dev RouterReceiver unchained initialization.
     * @param _router router role
     */
    function __RouterReceiver_init_unchained(address _router) internal {
        _grantRole(ROUTER_ROLE, _router);
    }

    /**
     * @dev Returns OpenGSN contract version (used for compatibility checks)
     */
    function versionRecipient() external pure virtual returns (string memory) {
        return '2.2.6';
    }

    /**
     * @dev Determine is an address a GSN trusted forwarder.
     * @param forwarder address to query
     * @return OpenGSN trusted forwarder status
     */
    function isTrustedForwarder(address forwarder) public view returns (bool) {
        return hasRole(ROUTER_ROLE, forwarder);
    }

    /**
     * @notice the following 3 functions are all required for OpenGSN integration
     * @dev Support for meta transactions
     * @return ret either msg.sender or user who called transaction through a relayer
     */
    function _msgSender() internal view virtual override returns (address ret) {
        if (msg.data.length >= 20 && hasRole(ROUTER_ROLE, msg.sender)) {
            // At this point we know that the sender is a trusted forwarder,
            // so we trust that the last bytes of msg.data are the verified sender address.
            // extract sender address from the end of msg.data
            assembly {
                ret := shr(96, calldataload(sub(calldatasize(), 20)))
            }
        } else {
            ret = msg.sender;
        }
    }

    /**
     * @dev Support for meta transactions
     * @return msgData from either msg.sender or from user who called through relayer
     */
    function _msgData() internal view virtual override returns (bytes calldata) {
        if (msg.data.length >= 20 && hasRole(ROUTER_ROLE, msg.sender)) {
            return msg.data[0:msg.data.length - 20];
        } else {
            return msg.data;
        }
    }

    /**
     * @dev ERC165 Support
     * @param interfaceId XOR of the external functions of the interface
     * @return bool whether interface is supported
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IRouterReceiver).interfaceId || super.supportsInterface(interfaceId);
    }
}
